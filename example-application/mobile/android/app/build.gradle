plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

android {
    namespace = "dev.vegito.app.android"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "dev.vegito.app.android"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdkVersion flutter.minSdkVersion       
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        ndk {
            // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            abiFilters 'arm64-v8a', 'x86_64'
        }
    }

    signingConfigs {
        release {
            def keystoreB64File = System.getenv("VEGITO_EXAMPLE_APPLICATION_MOBILE_ANDROID_RELEASE_KEYSTORE_BASE64_PATH")
                    ?: { throw new GradleException("Missing VEGITO_EXAMPLE_APPLICATION_MOBILE_ANDROID_RELEASE_KEYSTORE_BASE64_PATH") }()
            def storePassB64File = System.getenv("VEGITO_EXAMPLE_APPLICATION_MOBILE_ANDROID_RELEASE_KEYSTORE_STORE_PASS_BASE64_PATH")
                    ?: { throw new GradleException("Missing VEGITO_EXAMPLE_APPLICATION_MOBILE_ANDROID_RELEASE_KEYSTORE_STORE_PASS_BASE64_PATH") }()
            def keyPassB64File = System.getenv("VEGITO_EXAMPLE_APPLICATION_MOBILE_ANDROID_RELEASE_KEYSTORE_STORE_PASS_BASE64_PATH")
                    ?: { throw new GradleException("Missing VEGITO_EXAMPLE_APPLICATION_MOBILE_ANDROID_RELEASE_KEYSTORE_STORE_PASS_BASE64_PATH") }()
            def keyAliasValue = System.getenv("VEGITO_EXAMPLE_APPLICATION_MOBILE_ANDROID_RELEASE_KEYSTORE_ALIAS_NAME")
                    ?: { throw new GradleException("Missing VEGITO_EXAMPLE_APPLICATION_MOBILE_ANDROID_RELEASE_KEYSTORE_ALIAS_NAME") }()

            // --- keystore (Ã©crit dans buildDir) ---
            def keystoreFile = new File(buildDir, "release.keystore")
            keystoreFile.parentFile.mkdirs()

            if (!keystoreFile.exists()) {
                def b64 = new File(keystoreB64File).getText("UTF-8").trim()
                keystoreFile.bytes = b64.decodeBase64()
            }

            // --- passwords ---
            def storePasswordDecoded = new String(
                    new File(storePassB64File).text.decodeBase64()
            ).trim()

            def keyPasswordDecoded = new String(
                    new File(keyPassB64File).text.decodeBase64()
            ).trim()

            storeFile keystoreFile
            storePassword storePasswordDecoded
            keyAlias keyAliasValue
            keyPassword keyPasswordDecoded
        }
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
        }
    }
}

flutter {
    source = "../.."
}
